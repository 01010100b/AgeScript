Function Bool BuildPoint(Bool escrow, Int[2] point, Int distance, Int building_id):
// like up-build place-control but using build-line so no building queues getting stuck or weird stuff with target-point

Int attempt
Int max_attempts
Int max_delta
Int delta
Int[2] test_point
Int test_distance

max_attempts = 100

if LessThanOrEquals(distance, 3)
	max_attempts = 20
endif

if LessThanOrEquals(distance, 2)
	max_attempts = 10
endif

max_delta = Mul(distance, 2)
max_delta = Add(max_delta, 1)
attempt = 0

while LessThan(attempt, max_attempts)
	delta = GetRandom(max_delta)
	test_point[0] = Sub(point[0], distance)
	test_point[0] = Add(test_point[0], delta)
	delta = GetRandom(max_delta)
	test_point[1] = Sub(point[1], distance)
	test_point[1] = Add(test_point[1], delta)
	test_distance = GetPointDistance(point, test_point)
	
	if LessThanOrEquals(test_distance, distance)
		if CanBuildLine(escrow, test_point, building_id)
			BuildLine(test_point, building_id)
			
			return true
		endif
	endif
		
	attempt = Add(attempt, 1)
endwhile

ChatDataToSelf("failed placement %d", building_id)

return false

Function Bool BuildDropsite(Bool escrow, Int[2] point, Int distance, Int dropsite_id, Int resource):
// point and distance define a region in which the dropsite may be placed

Int[4] search_state
Int index
Int attempt

SetTargetPoint(point)
FullResetSearch()
FilterDistance(-1, distance)

if Equals(resource, RESOURCE_WOOD)
	FilterStatus(STATUS_READY, LIST_ACTIVE)
else
	FilterStatus(STATUS_RESOURCE, LIST_ACTIVE)
endif

search_state = FindResource(resource, 100)

if LessThanOrEquals(search_state[SEARCH_REMOTE_TOTAL], 0)
	return false
endif

if Equals(resource, RESOURCE_WOOD)
	
	if LessThanOrEquals(search_state[SEARCH_REMOTE_TOTAL], 30)
		return false
	endif
	
	CleanSearch(SEARCH_REMOTE, OBJECT_DATA_DISTANCE, SEARCH_ORDER_ASC)
	RemoveObjects("<", SEARCH_REMOTE, OBJECT_DATA_INDEX, 10)
endif

attempt = 0

while LessThan(attempt, 20)
	index = GetRandom(search_state[SEARCH_REMOTE_TOTAL])
	SetTargetObject(SEARCH_REMOTE, index)
	point = GetPoint(POSITION_OBJECT)
	distance = 0
	
	while LessThanOrEquals(distance, 3)
		
		if BuildPoint(true, point, distance, dropsite_id)
			return true
		endif
		
		distance = Add(distance, 1)
	endwhile
	
	attempt = Add(attempt, 1)
endwhile

ChatDataToSelf("failed placement %d", dropsite_id)

return false
