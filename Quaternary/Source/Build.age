Function Void DoBuilding():

Build_Housing()
Build_Dropsites()

Function Void Build_Housing():

Int player
Int pop_headroom
Int housing_headroom
Bool pending
Int[2] point

player = GetConstant("my-player-number")
pop_headroom = GetPlayerFact(player, FACT_POPULATION_HEADROOM, 0)
housing_headroom = GetPlayerFact(player, FACT_HOUSING_HEADROOM, 0)

if LessThan(pop_headroom, 1)
	return
endif

if GreaterThan(housing_headroom, 5)
	return
endif

pending = PendingObjects(">=", HOUSE, 1)

if pending
	return
endif

point = GetPoint(POSITION_SELF)
UtilsBuildPoint(true, point, 12, 6, 0, HOUSE)

Function Void Build_Dropsites():

Bool seen
Bool lack
Int[2] point
Bool success

// first lumber camp

seen = ResourceFound(RESOURCE_WOOD)
lack = ObjectTypeCountTotal("<", LUMBER_CAMP, 1)

if And(seen, lack)
	point = GetPoint(POSITION_SELF)
	success = UtilsBuildDropsite(true, point, 30, LUMBER_CAMP, RESOURCE_WOOD)
endif

if lack
	return
endif

// first mill

seen = ResourceFound(RESOURCE_FOOD)
lack = ObjectTypeCountTotal("<", MILL, 1)

if And(seen, lack)
	point = GetPoint(POSITION_SELF)
	success = UtilsBuildDropsite(true, point, 20, MILL, RESOURCE_FOOD)
endif

if lack
	return
endif

// add some farms

lack = ObjectTypeCountTotal("<", FARM, 3)

if lack
	success = UtilsBuildFarm(true, 5)
	
	if Not(success)
	
		if PendingObjects("<", MILL, 1)
			point = GetPoint(POSITION_SELF)
			UtilsBuildPoint(true, point, 20, 0, 0, MILL)
		endif
		
	endif
	
	return
endif
